### Controls ####
# on
# 5
# off
# quit or end or exit
# start or init
# no argument will toggle Caffeine on and off
################################
# On Indefinitely: caff on
# On for 5 Minutes: caff 5
# Off: caff off
# Quit App: caff quit or caff end or caff exit
# Start App: caff start or caff init
# Toggle: caff
################################
# If you turn caffeine on and it's not started it will
# start automatically for you first. No need to call
# caff start first.

on sendMsg(msg)
	tell application "System Events"
		set isRunning to (count of (every process whose bundle identifier is "com.Growl.GrowlHelperApp")) > 0
	end tell
	
	if isRunning then
		tell application id "com.Growl.GrowlHelperApp"
			set the allNotificationsList to {"Caffeine"}
			set the enabledNotificationsList to {"Caffeine"}
			register as application "Caffeine" all notifications allNotificationsList default notifications enabledNotificationsList icon of application "Caffeine"
			notify with name "Caffeine" title "Caffeine" description msg application name "Caffeine" icon of application "Caffeine"
		end tell
	end if
end sendMsg

on alfred_script(q)
	
	set msg to ""
	
	tell application "Caffeine"
		if q is equal to "quit" or q is equal to "end" or q is equal to "exit" then
			set msg to "Bye Bye, see you next time."
			quit
		else if q is equal to "" or q is missing value then
			set toggle to not active
			if toggle is true then
				set msg to "Turned on indefinitely"
				turn on
			else
				set msg to "Turned off"
				turn off
			end if
		else if q is equal to "on" or q is equal to "init" then
			set msg to "Turned on indefinitely"
			turn on
		else if q is equal to "off" then
			set msg to "Turned off"
			turn off
		else if q is equal to "start" or q is equal to "init" then
			set msg to "Application started"
			activate
		else
			try
				(q as real) div 1
				turn on for (q * 60)
				if q is equal to "1" then
					set s to ""
				else
					set s to "s"
				end if
				set msg to "Turned on for " & q & " minute" & s
			on error
				set msg to "Turned on indefinitely"
				turn on
			end try
		end if
	end tell
	
	if msg is not equal to "" then
		sendMsg(msg)
	end if
	
end alfred_script